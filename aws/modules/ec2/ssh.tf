# Generates a secure private key and encodes it in PEM (RFC 1421) and OpenSSH
# PEM (RFC 4716) formats. This resource is primarily intended for easily
# bootstrapping throwaway development environments.
#
# !!!WARNING!!!
# The private key generated by this resource will be stored unencrypted in your
# Terraform state file. Use of this resource for production deployments is not
# recommended. Instead, generate a private key file outside of Terraform and
# distribute it securely to the system where Terraform will be run.
resource "tls_private_key" "this" {
  count     = local.create_key_pair ? 1 : 0
  algorithm = "RSA"
  rsa_bits  = 4096
}

resource "local_file" "tf_key" {
  count    = local.create_key_pair ? 1 : 0
  content  = tls_private_key.this[0].private_key_pem
  filename = var.private_key_path
}

# Provides an EC2 key pair resource. A key pair is used to control login access
# to EC2 instances.
# Currently this resource requires an existing user-supplied key pair. This key
# pair's public key will be registered with AWS to allow logging-in to EC2
# instances.
resource "aws_key_pair" "this" {
  # (Required) The public key material
  public_key = local.create_key_pair ? tls_private_key.this[0].public_key_openssh : file(var.public_key_path)

  # (Optional) The name for the key pair. If neither key_name nor
  # key_name_prefix is provided, Terraform will create a unique key name using
  # the prefix terraform-.
  key_name = var.key_pair_name
}
